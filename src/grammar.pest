file		= { SOI ~ (assignment | output)* ~ EOI }

assignment	= { type ~ name ~ "=" ~ (initializer | name) }
output		= { "output" ~ name }
type		= { const_type | osc_type | amp_type | stereo_type | seq_type }
initializer 	= { const_initializer | block_initializer }
anonymous 	= { (const_type ~ const_initializer) | ((osc_type | amp_type | stereo_type | seq_type) ~ block_initializer) }
name		= @{ (ASCII_ALPHANUMERIC | "_" | "-")+ }

const_type 	= @{ "constant" | "const" }
osc_type	= @{ "oscillator" | "osc" }
amp_type	= @{ "amplifier" | "amp" }
stereo_type	= @{ "stereo" }
seq_type	= @{ "sequencer" | "seq" }

const_initializer	= @{ "-"? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }
block_initializer	= { "{" ~ (block_parameter ~ ",")* ~ block_parameter? ~ "}" }

block_parameter		= { parameter_name ~ ":" ~ parameter_value }
parameter_name		= @{ (ASCII_ALPHA | "_" | "-")+ ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)? }
parameter_value 	= _{ anonymous | waveform | sequence | name }

sequence		= { "[" ~ note* ~ "]" }
note			= @{ "-" | ("C" | "D" | "E" | "F" | "G" | "A" | "B") ~ ("#" | "b")? ~ ASCII_DIGIT? }

waveform		= { (waveform_sin | waveform_saw | waveform_sq | waveform_tri) ~ (WHITESPACE | ",")+ }
waveform_sin		= @{ "sinus" | "sin" }
waveform_saw		= @{ "sawtooth" | "saw" }
waveform_sq		= @{ "square" | "sq" }
waveform_tri		= @{ "triangle" | "tri" }

WHITESPACE = _{ " " | "\n" | "\t" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
